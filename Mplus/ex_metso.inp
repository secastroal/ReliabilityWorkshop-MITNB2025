TITLE: Example input file ME-TSO
DATA: FILE = "data.dat";
VARIABLE: 
NAMES = id time item1 item2 item3 item4 item5 item6 item7 item8 item9 item10; 
MISSING=.;
USEVAR = item1 item2 item3 item4 item5 item6 item7 item8 item9 item10;
CLUSTER = id;
TINTERVAL = time(1);

ANALYSIS:
TYPE = TWOLEVEL RANDOM;
ESTIMATOR = BAYES;
BITERATIONS = 10000(5000); ! Max and min number of iterations per chain.
PROCESSORS = 4; ! Number of processors.
CHAINS = 4; ! Number of chains. 
THIN = 1; ! Thinning parameter of MCMC. 
! Increase THIN to reduce autocorrelation of MCMC chains.

MODEL:

%WITHIN%

! Latent occasion specific variable.
eta BY item1@1
item2 (lamb2) 
item3 (lamb3)
item4 (lamb4)
item5 (lamb5)
item6 (lamb6)
item7 (lamb7)
item8 (lamb8)
item9 (lamb9)
item10 (&1 lamb10);

! Random autoregressive effect on the latent occasion specific variable
ar | eta ON eta&1 ;

! Variances of the random measurement error and 
! the latent state residual.
item1(o_err1) ;
item2(o_err2) ;
item3(o_err3) ;
item4(o_err4) ;
item5(o_err5) ;
item6(o_err6) ;
item7(o_err7) ;
item8(o_err8) ;
item9(o_err9) ;
item10(o_err10) ;
eta(s_var) ;

%BETWEEN%

! Latent trait-indicator variables
theta1 BY item1@1 ;
theta2 BY item2@1 ;
theta3 BY item3@1 ;
theta4 BY item4@1 ;
theta5 BY item5@1 ;
theta6 BY item6@1 ;
theta7 BY item7@1 ;
theta8 BY item8@1 ;
theta9 BY item9@1 ;
theta10 BY item10@1 ;

!Fix 0 intercepts and estimate latent trait-indicator means.
!Estimate random autoregressive effect mean.
[item1@0];
[item2@0];
[item3@0];
[item4@0];
[item5@0];
[item6@0];
[item7@0];
[item8@0];
[item9@0];
[item10@0];
[theta1*];
[theta2*];
[theta3*];
[theta4*];
[theta5*];
[theta6*];
[theta7*];
[theta8*];
[theta9*];
[theta10*];
[ar];

! Fix 0 between residual variance. Estimate latent trait-indicator
! variances and random coefficients variances.
! When using Bayesian estimation in Mplus, it is recommended to fix
! variances at a small number (e.g., 0.001) instead of 0.
item1@0.001;
item2@0.001;
item3@0.001;
item4@0.001;
item5@0.001;
item6@0.001;
item7@0.001;
item8@0.001;
item9@0.001;
item10@0.001;
theta1(t_var1);
theta2(t_var2);
theta3(t_var3);
theta4(t_var4);
theta5(t_var5);
theta6(t_var6);
theta7(t_var7);
theta8(t_var8);
theta9(t_var9);
theta10(t_var10);
ar;

! Covariances
theta1-theta10 WITH theta1-theta10 ar;

SAVEDATA: BPARAMETERS = metso_samples.dat; ! Save MCMC samples.
          ! Save plausible values of the factor scores and random effects
          FILE is metso_fscores.dat;
          SAVE = FSCORES (200 10);
          FACTORS = all;

DATA IMPUTATION: THIN = 10; ! Thinning for the imputation of the factor scores

OUTPUT: TECH8;

