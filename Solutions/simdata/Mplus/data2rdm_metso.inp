TITLE: Fit ME-TSO to simulated 2RDM data
DATA: FILE = "Mplus/data2rdm.dat";
VARIABLE: 
NAMES = id time I1 I2 I3 I4 I5 I6; 
MISSING=.;
USEVAR = I1 I2 I3 I4 I5 I6;
CLUSTER = id;
TINTERVAL = time(1);

ANALYSIS:
TYPE = TWOLEVEL RANDOM;
ESTIMATOR = BAYES;
BITERATIONS = 20000(10000);
PROCESSORS = 4;
CHAINS = 4;
THIN = 1;

MODEL:

%WITHIN%

! Latent state residual
eta BY I1@1
I2 (lamb2)
I3 (lamb3)
I4 (lamb4)
I5 (lamb5)
I6 (&1 lamb6);

! Random autoregressive effect on the latent state residual
ar | eta ON eta&1;

! Variances of the random measurement error and 
! the latent state residual.
I1(o_err1) ;
I2(o_err2) ;
I3(o_err3) ;
I4(o_err4) ;
I5(o_err5) ;
I6(o_err6) ;
eta(s_var) ;

%BETWEEN%

! Latent trait-indicator variables
theta1 BY I1@1 ;
theta2 BY I2@1 ;
theta3 BY I3@1 ;
theta4 BY I4@1 ;
theta5 BY I5@1 ;
theta6 BY I6@1 ;

! Fix at 0 intercepts and estimate latent trait-indicator means.
! Estimate random coefficients means
[I1@0];
[I2@0];
[I3@0];
[I4@0];
[I5@0];
[I6@0];
[theta1*];
[theta2*];
[theta3*];
[theta4*];
[theta5*];
[theta6*];
[ar];

! Fix at 0 between residual variance. Estimate latent trait-indicator
! variances and random coefficients variances.
I1@0.001;
I2@0.001;
I3@0.001;
I4@0.001;
I5@0.001;
I6@0.001;
theta1(t_var1);
theta2(t_var2);
theta3(t_var3);
theta4(t_var4);
theta5(t_var5);
theta6(t_var6);

! Covariances
theta1-theta6 WITH theta1-theta6 ar;

SAVEDATA: BPARAMETERS = data2rdm_metso_samples.dat;
          FILE is data2rdm_metso_fscores.dat;
          SAVE = FSCORES (200 10);
          FACTORS = all;

DATA IMPUTATION: THIN = 10;

OUTPUT: TECH8;

